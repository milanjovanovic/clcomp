(in-package #:clcomp)

(defun dump-hex-code (assembled)
  (apply #'concatenate 'string (mapcar #'byte-hex (reduce #'append assembled))))

(defun add-offset (hash offset)
  (maphash (lambda (k v)
	     (setf (gethash k hash) (+ v offset)))
	   hash))

;;; to simplify things we are encoding our jump as near jumps so they are always 6 bytes long
(defun set-jump-offsets (instructions direction)
  (let ((labels-offsets (make-hash-table))
	(new-instructions nil))
    (dolist (inst instructions)
      (cond ((eq (first inst) :label)
	     (setf (gethash (second inst) labels-offsets) 0)
	     (push inst new-instructions))
	    ((eq (first inst) :jump-fixup)
	     (let ((label-offset (gethash (third inst) labels-offsets)))
	       (if label-offset
		   (push (list (second inst)
			       (if (eq direction :normal)
				   (- label-offset)
				   label-offset))
			 new-instructions)
		   (push inst new-instructions))
	       (add-offset labels-offsets (jump-instruction-size inst))))
	    (t
	     (add-offset labels-offsets (instruction-size inst))
	     (push inst new-instructions))))
    new-instructions))

(defun jump-instruction-size (instruction)
  (let ((mnemonic (second instruction)))
    (cond ((eq mnemonic :jmp) 5)
	  (t 6))))

(defun instruction-size (instruction)
  (let ((f (first instruction)))
    (cond ((eq f :jump-fixup)
	   (jump-instruction-size instruction))
	  ((eq f :rip-relative-fixup)
	   7)
	  ((eq f :label)
	   0)
	  ((eq f :rip-relative-fixup-location)
	   *word-size*)
	  ((numberp f)
	   (length instruction))
	  (t (length (assemble-instruction instruction))))))

(defun code-size (instructions)
  (let ((size 0))
    (dolist (inst instructions)
      (setf size
	    (+ size (instruction-size inst))))
    size))

(defun assembly-pass-1 (instructions)
  (let (code)
    (dolist (inst instructions)
      (let ((mnemonic (first inst)))
	(cond ((eq mnemonic :rip-relative-fixup)
	       (push inst code))
	      ((eq mnemonic :label))
	      (t (push (assemble-instruction inst) code)))))
    (reverse code)))

(defun resolve-assembly-jumps (instructions)
  (set-jump-offsets (set-jump-offsets instructions :normal) :reverse))
